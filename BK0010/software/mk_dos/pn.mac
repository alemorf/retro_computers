.LINK 1000
V30=400		TEKPAN=402	PRENT=404	INSERT=406
INSFIL=410	STEN=125672	STENA=125674	ODIN=412
INREAD=414	INWRIT=416	REST=420	PERV=422
ESTDIR=424	TEKZAP=426	STBLK=430	LENC=432
LENBLK=434	GDE=436		PROC=440	FF=442
WREG=444	PARAM=125512	COLOR=446

VERS=120004	BLK=120006	SUBTEK=120010	SUBOLD=120011
DEVTEK=120012	SUBDIR=120013	DIR=120014	PANEL=120072
SCREEN=120074	STEK=120076	USER=120100	USER2=120114
OVERWR=120124	NAM=306		BLOK=314	SIZE=316
LD0=120134	LD1=120136	LD2=120140
SORTIR=120132	SY=120130	PALET=120127
NONDOS=120116
INIDR.=216	EMT6.=220	INOUT.=222	RDDIR.=224
DIRFI.=226	DIRPR.=230	FRFIN.=232	CREAT.=234
DELET=120036	SAVDI.=242	EMT20.=244	PRINA.=246
TXT=120050	OCTOU.=256	DECOU.=260	TST.=262
TST2.=264	ERROR.=266	FRFN2.=320	CRET2.=322

SELP$=0		SWAP$=2		RAMA$=4		TXT$=6
TXTA$=10	RDIR$=12	OUTP$=14	INVAD$=16
INVKR$=20	BIT16$=22	STA$=24		STA1$=26
WIN$=30		LIN$=32		KEY$=34		STR$=36
SORT$=40	FINS$=42	TX$=44		STEI$=46
TAKE$=50	ZWE$=52		NEBO$=54	CLS$=56
STWIN$=60	INF$=62		OTH$=64		OTHE$=66
ZWUK$=70	SDIR$=72	DEC$=74		C124$=76
C120$=100	IS$=102		IS1$=104	AD160$=106
OVL$=110
	BR	00STA
PARP:	.WORD	0
PODKEY:	.WORD	0
00STA:	CALL	ZAPUSK

STAR:	TST	@#177662
	NOP
	TRAP	BIT16$
	BEQ	STAR
	CLR	@#104
	CLR	@#122
START:	MOV	#1000,SP
	CLR	@#177130
	CLR	@BLK
	TRAP	BIT16$
	BNE	KEY
	MOV	#3000,R0
	TST	(PC)+
PRIZN:	.WORD	0
	BNE	2KEY
2:	TRAP	BIT16$
	BNE	KEY
	SOB	R0,2
	MOV	#1,PRIZN
2KEY:	MOVB	@#104,R0
	BR	1KEY
KEY:	CLR	PRIZN
	TRAP	INF$
	MTPS	#0
	MOV	#10000,-(SP)
022:	TST	@#122
	BNE	7K
	TSTB	@#105
	BNE	023
	MOV	PARP,R0
	BEQ	026
	CALL	@R0
	BR	025
026:	TRAP	INF$
	MTPS	#0
025:	DEC	@SP
	BNE	022
	JMP	NEBO
023:	EMT	6
	MTPS	#340
1KEY:	MOV	PODKEY,R3
	BEQ	0TRABL
	CALL	@R3
0TRABL:	MOV	#TABKEY,R1
	MOV	#TABJMP,R2
3K:	TSTB	@R1
	BEQ	4K
	CMPB	(R1)+,R0
	BEQ	0OK
	TST	(R2)+
	BR	3K
0OK:	TRAP	SELP$
	MOV	@R2,-(SP)
	TST	14(R5)
	RET
7K:	EMT	6
	MTPS	#340
4K:	MOV	#STEN,R1
	MOV	#STENA,R2
	ADD	@R1,R2
	MOV	#75200+4,@#160
	ADD	@R1,@#160
	CMPB	R0,#30
	BNE	5K
	TST	@R1
	BEQ	START
	DEC	@#160
	MOVB	#40,R0
	EMT	16
	EMT	16
	DEC	@R1
	TRAP	AD160$	.WORD	-2
	BR	KRO
5K:	CMP	@R1,#47
	BLO	6K
	TRAP	STA1$
6K:9K:	EMT	TST.
	MOVB	R0,@R2
10K:	INC	@R1
	EMT	16
KRO:	CALL	KURSS
	MOV	#6000,R0
	SOB	R0,.
	TRAP	STA$


TABKEY:.BYTE	200,14,220,26,27,11,33,32,10,31,12,221
.BYTE	2,6,23,273,275,206,222,201,202,203,204,205,207,210,211
.BYTE	212,3,230,231,232,233,223,224,0
.EVEN
TABJMP:.WORD QUIT,REREAD,DRIVE,LEFT,RIGHT,TAB,DOWN,UP,LEVO
.WORD	PRAVO,ENTER,FILTER,MKBRF,MKFUL,WS,PLUS,MINUS
.WORD	RENAME,PALIT,HELP,USR,VIEW,EDIT,COPY,MKDIR,DELETE,MENU
.WORD	CNTRL,MENU,LEVO,PRAVO,LEVO,PRAVO,COMPAR,FILER
TABTRP:.WORD	SELPAN,SWAPAN,RAMKA,OUTT,OUTT1,RDIR,OUTPAN
.WORD	INV0,INV1,BIT1,START,STAR,WIN,LINII,TSTKEY,STROKA
.WORD	SORT,FINSUB,TX,STEIT,TAKE,ZWEZ,NEBO1,CLS,STWIN,INFOR
.WORD	OTHE,OTHER,ZWUKI,SAVED,DECOUT,C124,C120,IS,IS1,AD160
.WORD	OVL
AD160:	ADD	@(SP)+,@#160
	ADD	#2,-(SP)
	RET

MKBRF:	TRAP	INVKR$
	BISB	#4,SORTIR
	BR	1
MKFUL:	TRAP	INVKR$
	BICB	#4,SORTIR
1:	TRAP	SORT$
	TRAP	LIN$
	TRAP	OUTP$
IS1:	TRAP	INVKR$
	TRAP	STA1$
C120:	CMP	12(R5),10(R5)
	RET
C124:	CMP	12(R5),4(R5)
	RET
HLT:	SUB	@PC,@SP
	RTI
STWIN:	TRAP	INVKR$
	TRAP	SELP$
	COM	@#44
	MOV	@#160,R0
	ADD	#10006,R0
	TRAP	WIN$
	.BYTE	22,36
	RET

BIT1:	BIT	#100,@#177716
	RET

FILTER:	BEQ	111
	TRAP	STA1$
111:	TRAP	STWIN$
	TRAP	TX$
	.WORD	10515	.ASCIZ/Filter/	.EVEN
	CLR	@#44
	TRAP	SELP$
	TRAP	AD160$	.WORD	10511+1300
	MOV	R5,R3
	ADD	#20,R3
	MOV	#16,R2
	TRAP	STR$
	BCS	1
	TRAP	SELP$
	CMPB	20(R5),#40
	BHI	55
	CLRB	20(R5)
4:	TRAP	TAKE$
	MOV	R3,-(SP)
	MOVB	1(R3),SUBDIR
	MOVB	1(R3),SUBTEK
	TRAP	SORT$
	MOV	(SP)+,R3
	TRAP	STEI$
	BR	1
55:	TRAP	SORT$

1:	TRAP	LIN$
	TRAP	SELP$
	TRAP	OUTP$
	TRAP	IS1$

STEIT:	MOV	R3,-(SP)
	TRAP	SELP$
	MOV	(SP)+,R3
	CLR	R1
	MOV	4(R5),R2
	BEQ	3
2:	CMP	(R4)+,R3
	BEQ	0OK
	INC	R1
	SOB	R2,2
	BR	3
0OK:	DEC	R1
	BMI	1
	INC	12(R5)
	BR	0OK
1:	TRAP	C120$
	BLOS	3
	INC	10(R5)
	INC	6(R5)
	BR	1
3:	RET

WS:	BNE	0ERA
	TRAP	TAKE$
	CMPB	2(R3),#177
	BEQ	1ERA
	CMP	R3,#LDISK
	BEQ	1ERA
	MOV	22(R3),R0
	MOV	#100000,R1
	XOR	R1,R0
	MOV	R0,22(R3)
	MOV	R3,-(SP)
	TRAP	INVKR$
	TRAP	SELP$
	BITB	#4,SORTIR
	BEQ	0F
	CALL	INV0
	MOV	R4,@#160
	MOV	(SP)+,R3
	CALL	PRNAME
	BR	2
0F:	CALL	INV0
	MOV	R4,@#160
	MOV	(SP)+,R3
	CALL	FLPRI
2:	TRAP	INVKR$
1ERA:	TRAP	SELP$
	JMP	DOWN


PRAVO:	TRAP	C124$
	BLO	1
0ERA:	TRAP	STA1$
1:	SWAB	R0
	TSTB	R0
	BNE	0BRF

	TRAP	C120$
	BEQ	2
1E:	TRAP	INVKR$
	MOV	10(R5),12(R5)
0E:	TRAP	IS$
2:	CMP	4(R5),#18.
	BHI	3
	TRAP	STA1$
3:	MOV	12(R5),R0
	ADD	#18.,R0
	CMP	R0,4(R5)
	BHI	4
	TRAP	INVKR$
	ADD	#18.,12(R5)
	ADD	#18.,6(R5)
	ADD	#18.,10(R5)
2E:	TRAP	OUTP$
	BR	0E
4:	TRAP	INVKR$
	MOV	4(R5),10(R5)
	MOV	4(R5),12(R5)
	MOV	4(R5),6(R5)
	SUB	#17.,6(R5)
	BR	2E
0BRF:	MOV	12(R5),R0
	SUB	6(R5),R0
	CMP	R0,#18.
	BHIS	01
	MOV	12(R5),R0
	ADD	#18.,R0
	CMP	R0,10(R5)
	BHIS	1E
	TRAP	INVKR$
	ADD	#18.,12(R5)
	BR	0E
01:	TRAP	C120$
	BLO	1E
	MOV	4(R5),10(R5)
	MOV	4(R5),12(R5)
	CMP	4(R5),#36.
	BHI	02
	MOV	#1,6(R5)
	BR	2E
02:	MOV	4(R5),6(R5)
	SUB	#35.,6(R5)
	BR	2E


LEVO:	CMP	12(R5),#1
	BHI	1
	TRAP	STA1$
1:	SWAB	R0
	TSTB	R0
	BNE	0BRF

	CMP	12(R5),6(R5)
	BEQ	2
1E:	TRAP	INVKR$
	MOV	6(R5),12(R5)
0E:	TRAP	IS$
2:	CMP	6(R5),#18.
	BHI	3
	TRAP	INVKR$
	MOV	#1,12(R5)
	MOV	#1,6(R5)
	MOV	#18.,10(R5)
	CMP	4(R5),#18.
	BHIS	4
	MOV	4(R5),10(R5)
4:	TRAP	OUTP$
	BR	0E
3:	TRAP	INVKR$
	SUB	#18.,6(R5)
	SUB	#18.,12(R5)
	SUB	#18.,10(R5)
	BR	4
0BRF:	MOV	12(R5),R0
	SUB	6(R5),R0
	CMP	R0,#18.
	BLO	01
	TRAP	INVKR$
	SUB	#18.,12(R5)
	BR	0E
01:	CMP	12(R5),6(R5)
	BNE	1E
	TRAP	SORT$
	TRAP	OUTP$
	TRAP	IS1$

UP:	CMP	12(R5),#1
	BHI	1
1E:	TRAP	STA1$
1:	SWAB	R0
	TSTB	R0
	BEQ	0FULL
	CMP	12(R5),6(R5)
	BLOS	0BN
	MOV	12(R5),R0
	SUB	6(R5),R0
	CMP	R0,#18.
	BNE	0FULL
	TRAP	INVKR$
	DEC	12(R5)
	TRAP	IS$
0BN:	CMP	12(R5),#1
	BLOS	1E
	TRAP	INVKR$
2B:	CMP	12(R5),#1
	BLOS	2KFL$
	CALL	DWIGUP
	TRAP	SELP$
	DEC	12(R5)
	DEC	6(R5)
	DEC	10(R5)
	TRAP	AD160$	.WORD	2201
	CALL	PRNAM$
	TRAP	SELP$
	TRAP	AD160$	.WORD	2221
	MOV	12(R5),-(SP)
	ADD	#18.,12(R5)
	CALL	PRNAM$
	MOV	(SP)+,12(R5)
	TRAP	SELP$
	TRAP	KEY$
	BCC	2B
	BR	2KFL$
0FULL:	CMP	12(R5),6(R5)
	BLOS	0NFL
	CALL	INVUP
	DEC	12(R5)
	TRAP	STA$
0NFL:	CMP	12(R5),#1
	BLOS	1E
	TRAP	INVKR$
1NFL:	CMP	12(R5),#1
	BLOS	2KFL$
	CALL	DWIGUP
	TRAP	SELP$
	DEC	12(R5)
	DEC	6(R5)
	DEC	10(R5)
	TRAP	AD160$	.WORD	2201
	CALL	FLPRI$
	TRAP	SELP$
	TRAP	KEY$
	BCC	1NFL
2KFL$:	BR	2KFL

DOWN:	TRAP	C124$
	BLO	1
1E:	TRAP	STA1$
1:	SWAB	R0
	TSTB	R0
	BEQ	0FULL

	TRAP	C120$
	BHIS	0BK
	MOV	12(R5),R0
	SUB	6(R5),R0
	CMP	R0,#17.
	BNE	0FULL
	TRAP	INVKR$
	INC	12(R5)
0WS:	TRAP	IS$
0BK:	TRAP	C124$
	BHIS	1E
	TRAP	INVKR$
1BK:	TRAP	C124$
	BHIS	2KFL
	CALL	DWIGDN
	TRAP	SELP$
	INC	12(R5)
	INC	6(R5)
	INC	10(R5)
	TRAP	AD160$	.WORD	2100+25321
	CALL	PRNAM$
	TRAP	SELP$
	TRAP	AD160$	.WORD	2100+25301
	MOV	12(R5),-(SP)
	SUB	#18.,12(R5)
	CALL	PRNAM$
	MOV	(SP)+,12(R5)
	TRAP	SELP$
	CMPB	@#104,#23
	BEQ	0WS
	TRAP	KEY$
	BCC	1BK
	BR	2KFL

0FULL:	TRAP	C120$
	BHIS	0KFL
	CALL	INVDN
	INC	12(R5)
	TRAP	STA$
0KFL:	TRAP	C124$
	BHIS	1E
	TRAP	INVKR$
1KFL:	TRAP	C124$
	BHIS	2KFL
	CALL	DWIGDN
	TRAP	SELP$
	INC	12(R5)
	INC	6(R5)
	INC	10(R5)
	TRAP	AD160$	.WORD	2100+25301
	CALL	FLPRI$
	TRAP	SELP$
	CMPB	@#104,#23
	BEQ	0WS
	TRAP	KEY$
	BCC	1KFL

2KFL:	TRAP	IS1$

TSTKEY:	TRAP	BIT16$
	BNE	3E
	TST	PRIZN
	BNE	1
	MOV	#3000,R0
2:	TRAP	BIT16$
	BNE	3E
	SOB	R0,2
	INC	PRIZN
1:	CLC
	RET
3E:	CLR	PRIZN
	SEC
	RET

DWIGDN:	MOV	@#160,R0
	ADD	#2100,R0
	MOV	R0,R2
	ADD	#1200,R2
	MOV	#100-40,R4
	BR	0DWIG
DWIGUP:	MOV	@#160,R2
	ADD	#2100+25200,R2
	MOV	R2,R0
	ADD	#1200,R0
	MOV	#-100-40,R4
0DWIG:	MOV	#171.,R1
1:	MOV	#4,R3
2:	MOV	(R2)+,(R0)+
	MOV	(R2)+,(R0)+
	MOV	(R2)+,(R0)+
	MOV	(R2)+,(R0)+
	SOB	R3,2
	ADD	R4,R0
	ADD	R4,R2
	SOB	R1,1
	RET


INVUP:	TRAP	INVAD$
1:	MOV	R1,R0
	MOV	#100,R3
	SUB	R1,R3
	SUB	R3,R4
2:	COMB	-(R4)
	COMB	1200(R4)
	SOB	R0,2
	MOV	#1100,R0
	SOB	R0,.
	SOB	R2,1
	RET

INVDN:	TRAP	INVAD$
1:	MOV	R1,R0
2:	COMB	1200(R4)
	COMB	(R4)+
	SOB	R0,2
	MOV	#1100,R0
	SOB	R0,.
	MOV	#100,R3
	SUB	R1,R3
	ADD	R3,R4
	SOB	R2,1
	RET

INV1:	TRAP	INVAD$
1:	MOV	R1,R0
2:	COMB	(R4)+
	SOB	R0,2
	ADD	#100,R4
	SUB	R1,R4
	SOB	R2,1
	RET

INV0:	TRAP	SELP$
	MOVB	DEVTEK,R0
	ADD	#101,R0
	MOV	#75200,@#160
	EMT	16
	TRAP	SELP$
	SWAB	R0
	MOV	@#160,R4
	ADD	#2201,R4
	MOV	R4,R1
	ADD	#20,R1
	MOV	12(R5),R2
	SUB	6(R5),R2
	BEQ	0OK
	CMP	R2,#18.
	BLO	1
	MOV	R1,R4
	SUB	#18.,R2
	BEQ	0OK
1:	ADD	#1200,R4
	SOB	R2,1
0OK:	MOV	#36,R1
	TSTB	R0
	BEQ	3
	MOV	#16,R1
3:	MOV	#12,R2
	RET

PRIP:	TRAP	SELP$
	MOVB	DEVTEK,R0
	ADD	#101,R0
	MOVB	R0,PRIVOD
	TRAP	TXTA$	.WORD	75200
PRIVOD:	.ASCIZ"A:\>"<11><11><11><11><11>	.EVEN
	CLR	STEN
	TRAP	AD160$	.WORD	-55
	JMP	KURSS

OUTPAN:	TRAP	SELP$
	TRAP	AD160$	.WORD	502
	CALL	PUTC
	TRAP	SELP$
	SWAB	R0
	TSTB	R0
	BNE	BREIF
	TRAP	AD160$	.WORD	2201
	CLR	F010+2
	MOV	6(R5),R2	;ξαώ.ζακμ
	BEQ	F010
1:	CMP	R2,10(R5)
	BHI	F010
	MOV	R2,R3
	DEC	R3
	ASL	R3
	ADD	R4,R3
	MOV	@R3,R3
	CALL	FLPRI
	INC	F010+2
	TRAP	AD160$	.WORD	1200-36
	INC	R2
	BR	1
F010:	MOV	#0,R0
	MOV	#18.,R3
	SUB	R0,R3
	BEQ	2
3:	MOV	#11+5,R1	;TRAP	TXT$
	CALL	CLL		;.BYTE	11,5,0	.EVEN
	INC	@#160
	MOV	#6,R1		;TRAP	TX$
	CALL	CLL		;.WORD	1	.BYTE	6,0	.EVEN
	INC	@#160		;TRAP	TX$
				;.WORD	1	.BYTE	6,0	.EVEN
	CALL	CLL
	TRAP	TX$
	.WORD	1	.BYTE	40,0	.EVEN
	TRAP	AD160$	.WORD	1200-36
	SOB	R3,3
2:	BR	INFOR

BREIF:	TRAP	AD160$	.WORD	2201
	MOV	@#160,-(SP)
	ADD	#20,@SP
	CLR	F020+2
	MOV	6(R5),R2
	BEQ	F020
1:	CMP	R2,10(R5)
	BHI	F020
	MOV	R2,R3
	DEC	R3
	ASL	R3
	ADD	R4,R3
	MOV	@R3,R3
	CALL	PRNAME
001:	TRAP	AD160$	.WORD	1200-16
	INC	F020+2
	CMP	F020+2,#18.
	BNE	4
	MOV	@SP,@#160
4:	INC	R2
	BR	1

F020:	MOV	#0,R0
	MOV	#36.,R3
	SUB	R0,R3
	BEQ	0EX
01:	CMP	R3,#18.
	BNE	02
	MOV	@SP,@#160
02:	MOV	#11+5,R1	;TRAP	TXT$
	CALL	CLL		;.BYTE	11,5,0	.EVEN
	TRAP	AD160$	.WORD	1200-16
	SOB	R3,01
0EX:	TST	(SP)+

INFOR:	TRAP	SELP$
	TRAP	AD160$	.WORD	31201
	MOV	14(R5),R5
	BEQ	2
	CMP	R5,#14
	BNE	1
	TRAP	TXT$
	.ASCIZ<5>/?MC - Non DOS disk./<6>	.EVEN
	RET
1:	TRAP	TXT$
	.ASCIZ<3>"Directory I/O error #"	.EVEN
	MOV	R5,R0
	JMP	DECOUT
2:	TRAP	SELP$
	TRAP	AD160$	.WORD	31201
	CLR	INSERT
	CLR	INSFIL
	MOV	30(R3),R2
	BEQ	3
	ADD	#450,R3
4:	ADD	#30,R3
	CMPB	#377,@R3
	BEQ	4
	CMPB	#177,2(R3)
	BEQ	5
	CMPB	#200,@R3
	BEQ	5
	MOV	22(R3),R1
	BPL	5
	BIC	#100000,R1
	ADD	R1,INSERT
	INC	INSFIL
5:	SOB	R2,4
3:	TST	INSFIL
	BEQ	TYPBRF
	TRAP	TXT$
	.ASCIZ<237>/ Selected:/	.EVEN
	MOV	INSFIL,R0
	TRAP	DEC$
	TRAP	TXT$
	.ASCIZ/Blocks:/	.EVEN
	MOV	INSERT,R0
	TRAP	DEC$
	TRAP	TXT$
	.BYTE	40,237,0	.EVEN
	RET

CLL:	MOV	@#160,R0
	ADD	R1,@#160
	MOV	#12,R2
1:	MOV	R1,R4
2:	CLRB	(R0)+
	SOB	R4,2
	ADD	#100,R0
	SUB	R1,R0
	SOB	R2,1
	RET

TYPBRF:	TRAP	SELP$
	TRAP	AD160$	.WORD	31201
	SWAB	R0
	TST	4(R5)
	BEQ	1T
	TSTB	R0
	BNE	1
1T:	TRAP	TXT$
	.ASCIZ/ Files:/	.EVEN	;7
	MOV	16(R5),R0
	TRAP	DEC$		;15
	TRAP	TXT$
	.ASCIZ/Blk:/	.EVEN	;21
	MOV	32(R3),R0
	TRAP	DEC$		;27
	TRAP	TXT$
	.ASCIZ"/"	.EVEN	;30
	MOV	466(R3),R0
	SUB	32(R3),R0
	JMP	DECOUT		;36
1:	MOV	@#160,-(SP)
	TRAP	TX$
	.WORD	16	.BYTE	40,0	.EVEN
	TRAP	TX$
	.WORD	6	.BYTE	40,0	.EVEN
	TRAP	TX$
	.WORD	6	.BYTE	40,0	.EVEN
	MOV	(SP)+,@#160

FLPRI$:	MOV	12(R5),R3
	DEC	R3
	BMI	00
	ASL	R3
	ADD	R4,R3
	MOV	@R3,R3

FLPRI:	CALL	PRNAME
	INC	@#160
	CMPB	#177,2(R3)
	BEQ	2
	CMPB	#2,@R3
	BNE	1
	TRAP	TXT$
	.ASCIZ/}DISK{/	.EVEN
	INC	@#160
	CMP	R3,#LDISK
	BNE	04
	TRAP	TXT$
	.ASCIZ/}ROOT{/	.EVEN
	BR	0
04:	MOV	22(R3),R0
	BIC	#100000,R0
	COMB	@#44
	TRAP	DEC$
	CLRB	@#44
	BR	0
2:	CMP	R3,#TOSH
	BNE	01
	TRAP	TXT$
	.ASCIZ/} UP {/	.EVEN
	BR	88
01:	TST	24(R3)
	BEQ	89
	TRAP	TXT$	.ASCIZ/}DEV {/	.EVEN
	TRAP	TX$	.WORD	1
	.BYTE	40,40,0		.EVEN
	MOV	24(R3),R0
	EMT	16
	TRAP	TXT$	.ASCIZ/:  /	.EVEN
	BR	0
89:	TRAP	TXT$
	.ASCIZ/}SUB {/	.EVEN
88:	TRAP	TX$
	.WORD	1
	.ASCIZ/}DIR {/	.EVEN
	BR	0
1:	MOV	R2,-(SP)
	MOV	24(R3),R2
	CALL	OCTOUT
	INC	@#160
	MOV	22(R3),R0
	BIC	#100000,R0
	CMP	R0,#70
	BLO	09
	COMB	@#44
	TRAP	DEC$
	CLRB	@#44
	BR	10
09:	MOV	26(R3),R2
	CALL	OCTOUT
10:	MOV	(SP)+,R2
	CMPB	#1,@R3
	BNE	0
	MOVB	#'P,R0
	BR	000
0:	MOV	#40,R0
000:	INC	@#160
	CALL	EMT16
00:	RET

DECOUT:	MOV	@#160,-(SP)
	ADD	#6,@SP
	MOV	R2,-(SP)
	MOV	R4,-(SP)
	EMT	DECOU.
	MOV	(SP)+,R4
	MOV	(SP)+,R2
	MOV	(SP)+,R1
1:	CMP	@#160,R1
	BHIS	2
3:	MOV	#40,R0
	CALL	EMT16
	BR	1
2:	RET

PRNAM$:	TRAP	TAKE$
PRNAME:	TST	22(R3)
	BPL	001
	COMB	@#44
001:	MOV	R1,-(SP)	;πεώατψ ινεξι ζακμα
	MOV	R2,-(SP)
	MOV	R3,R1
	TST	(R1)+
	MOV	#15,R2
	MOVB	(R1)+,R0
	CMPB	R0,#177
	BNE	1I
	MOV	#40,R0
1:	CALL	EMT16
2:	MOVB	(R1)+,R0
	CALL	@120062
	CALL	EMT16
	SOB	R2,2
	BR	1000
1I:	CMPB	#2,@R3
	BEQ	1
	CALL	EMT16
	MOV	R1,-(SP)
	MOV	R2,R0
	ADD	R0,R1
2I:	CMPB	-(R1),#'.
	BEQ	3I
	SOB	R0,2I
4I:	MOV	(SP)+,R1
	BR	2
3I:	CMPB	4(R1),#40
	BHI	4I
	TST	R0
	BEQ	4I
5I:	CMPB	-(R1),#'.
	BEQ	4I
	SOB	R0,5I
	TST	(SP)+
	MOV	R3,R1
	TST	(R1)+
	INC	R1
1$:	MOVB	(R1)+,R0
	CALL	@120062
	CMPB	R0,#56
	BNE	2$
	CMPB	@R1,#100
	BLO	2$
	CMPB	3(R1),#40
	BHI	2$
3$:	CMP	R2,#4
	BLOS	2$
	MOV	R0,-(SP)
	MOV	#40,R0
	CALL	EMT16
	DEC	R2
	MOV	(SP)+,R0
	BR	3$
2$:	CALL	EMT16
	SOB	R2,1$




1000:	TST	22(R3)
	BPL	002
	MOV	@#160,R2
	DEC	R2
	MOV	#11,R1
003:	COMB	@R2
	ADD	#100,R2
	SOB	R1,003
	CLRB	@#44
002:	MOV	(SP)+,R2
	MOV	(SP)+,R1
	RET

TAKE:	MOV	12(R5),R3
	DEC	R3
	ASL	R3
	ADD	R4,R3
	MOV	@R3,R3
	RET

OUTDRV:	MOV	@#160,-(SP)
	COMB	@#44
	MOV	#6,R2
	MOV	(PC)+,R1
NAP:	.WORD	0
1:	MOV	R1,R0
	ADD	#101,R0
	EMT	16
	MOVB	#':,R0
	EMT	16
	INC	@#160
	INC	R1
	SOB	R2,1
	CLRB	@#44
	MOV	(SP)+,@#160
	RET

DRCOM:	MOV	@#160,R3
	MOVB	DEVTEK,R2
	SUB	@R4,R2
	BEQ	1
2:	ADD	#3,R3
	SOB	R2,2
1:	MOV	#12,R2
3:	COMB	@R3
	COMB	1(R3)
	ADD	#100,R3
	SOB	R2,3
	RET

LEFT:	TST	TEKPAN
	BEQ	DRIVE
	BR	1
RIGHT:	TST	TEKPAN
	BNE	DRIVE
1:	TRAP	INVKR$
	TRAP	SWAP$
	TST	(PC)+
DRIVE:	TRAP	INVKR$
	TRAP	SELP$
	COMB	@#45
	TRAP	AD160$	.WORD	10005
	MOV	@#160,R0
	TRAP	AD160$	.WORD	1502
	TRAP	WIN$
	.BYTE	23,36
	MOV	#NAP,R4
1K:	CLR	@R4
	CMPB	DEVTEK,#6
	BLO	1
	MOVB	DEVTEK,@R4
	SUB	#5,@R4
1:	CALL	OUTDRV
0K:	CALL	DRCOM
	EMT	EMT6.
	CALL	DRCOM
	MOV	#DEVTEK,R3
	CMPB	R0,#12
	BEQ	2
	CMPB	R0,#3
	BNE	3
	CLRB	@#45
	BR	CLOW
3:	CMPB	R0,#10
	BNE	4
	TSTB	@R3
	BEQ	0K
	CMPB	@R3,@R4
	BNE	31
	DEC	@R4
31:	DECB	@R3
	BR	1
4:	CMPB	R0,#31
	BNE	5
	CMPB	@R3,#'Z-101
	BHIS	0K
	MOV	@R4,R0
	ADD	#5,R0
	CMPB	@R3,R0
	BLO	41
	INC	@R4
41:	INCB	@R3
	BR	1
5:	CMPB	R0,#212
	BNE	90
	MOVB	@R3,@#37777
	TRAP	CLS$
	CMPB	#300,@#177717
	BNE	92
	CALL	@#150170
	BR	91
92:	CALL	@#100140
91:	MOVB	@#37777,R0
	JMP	@#160002
90:	EMT	TST2.
	SUB	#101,R0
	BMI	0K
	CMPB	R0,#'Z-101
	BHI	0K
	MOVB	R0,@R3
	NOP

2:	CLRB	@#45
	MOV	#PANEL,R0
	TST	TEKPAN
	BEQ	9
	INC	R0
9:	MOVB	@R3,@R0
	TST	(PC)+

REREAD:	TRAP	INVKR$
	CALL	SBROS
CLOW:	TRAP	LIN$
	TRAP	OUTP$
RETMY:	TRAP	IS1$

TAB:	TRAP	INVKR$
	TRAP	SWAP$
	TRAP	IS1$


SBROS:	TRAP	SELP$
	CLRB	SUBTEK
	CLRB	SUBDIR
	CLR	LD0
	CLR	LD1
RDIR:	TRAP	SELP$		;ώτεξιε λαταμοηα σ σοςτ.
	EMT	INIDR.
	EMT	RDDIR.
	BCS	BERR
	CMP	400(R3),#123456
	BEQ	2
	MOVB	#14,@#52
	MOV	NONDOS,R0
	BEQ	BERR
	CLRB	@#52
	TST	(R0)+
	CALL	@R0
	BCC	2
BERR:	TRAP	SELP$
	MOV	R5,R4
	CMP	(R5)+,(R5)+	;οώιστλα πας-οχ παξεμι
	MOV	#6+7,R2		;ι ζιμψτςα
	CLR	(R5)+
	SOB	R2,.-2
	CMP	-(R5),-(R5)
	MOVB	@#52,14(R4)
	SEC
	RET
2:	CALL	BERR
	TRAP	SELP$
	CLR	14(R5)		;=εστψ διςελτοςικ
	BIC	#177400,30(R3)
	CLRB	SUBTEK
	CLRB	SUBDIR

SORT:	TRAP	SELP$
	CLR	4(R5)		;λ-χο ζακμοχ,LD ι DIR
	CLR	6(R5)		;ξαώ.ζακμ ξα παξεμι
	CLR	10(R5)		;λοξ.ζακμ ξα παξεμι
	CLR	12(R5)		;τελ.ζακμ ξA παξεμι
	CLR	16(R5)		;λ-χο ζακμοχ ξα υστςοκστχε
	TST	14(R5)
	BEQ	1
	SEC			;εσμι λαταμοηα ξετυ
	RET
1:	MOV	30(R3),R2
	BNE	11OK
	TST	LD0
	BEQ	0OK
	MOV	#LDISK,(R4)+
	INC	4(R5)
	BR	0OK
11OK:	ADD	#450,R3
	CMPB	20(R5),#40
	BLOS	2

0F:	ADD	#30,R3		;σοςτιςοχλα σ ζιμψτςον...
	CMPB	#377,@R3
	BEQ	0F
	TSTB	@R3
	BEQ	0FOK
	CMPB	#1,@R3
	BNE	1FOK
0FOK:	CMPB	#177,2(R3)
	BEQ	1FOK
	MOV	R3,-(SP)
	TST	(R3)+
	MOV	R5,R1
	ADD	#20,R1
	MOV	#16,-(SP)
1F:	MOVB	(R3)+,R0
	CALL	@120064
	CMPB	(R1),#40
	BLOS	0MOV
	CMPB	@R1,#'*
	BEQ	0MOV
	CMPB	@R1,#'.
	BNE	2F
	CMPB	R0,#'.
	BEQ	2F
	DEC	@SP
	BNE	1F
	BR	0EF
2F:	MOVB	R0,-(SP)
	MOVB	(R1)+,R0
	TSTB	-(SP)
	CALL	@120064
	TSTB	(SP)+
	CMPB	R0,(SP)+
	BNE	0EF
	DEC	@SP
	BNE	1F
0MOV:	TST	(SP)+
	MOV	(SP)+,R3
	MOV	R3,(R4)+
	INC	4(R5)
	BR	1FOK
0EF:	TST	(SP)+
	MOV	(SP)+,R3
1FOK:	SOB	R2,0F
	MOV	4(R5),16(R5)

0OK:	TRAP	SELP$
	INC	6(R5)
	INC	12(R5)
	MOV	#36.,R1
	SWAB	R0
	TSTB	R0
	BNE	0BREIF
	ASR	R1
0BREIF:	CMP	4(R5),R1
	BLO	0BR
	MOV	R1,10(R5)
	BR	0BR1
0BR:	MOV	4(R5),10(R5)
0BR1:	CLC
	RET
2OK:	BR	0OK
2:	TSTB	SUBTEK
	BEQ	3
	MOV	#TOSH,(R4)+
	BR	5
3:	TST	LD0
	BEQ	4
	MOV	#LDISK,(R4)+
5:	INC	4(R5)
4:	MOV	#FILES,R1
	CLR	(R1)+

6:	ADD	#30,R3
	CMPB	#377,@R3
	BEQ	6
	CMPB	2(R3),#177
	BEQ	7
	CMPB	@R3,#2
	BNE	8
7:	CMPB	SUBTEK,1(R3)
	BNE	0R
	MOV	R3,(R4)+
	INC	4(R5)
	BR	0R
8:	CMPB	#200,@R3
	BEQ	0R
	INC	16(R5)
	CMPB	SUBTEK,1(R3)
	BNE	0R
	MOV	R3,(R1)+
	INC	FILES
0R:	SOB	R2,6
	MOV	#FILES,R3
	MOV	(R3)+,R2
	BEQ	0OK
	BIC	#177774,R0
	ASL	R0
	ADD	R0,PC
	BR	0UNSRT
	BR	0NAME

0EXT:	MOV	(R3)+,R1
	MOV	R2,-(SP)
	MOV	#16,R2
	TST	(R1)+
010:	CMPB	(R1)+,#'.
	BEQ	020
	SOB	R2,010
	MOV	-(R3),(R4)+
	INC	4(R5)
	TST	(R3)+
020:	MOV	(SP)+,R2
	SOB	R2,0EXT

	MOV	#41,-(SP)
0E:	MOV	#FILES,R3
	MOV	(R3)+,R2
0E1:	MOV	(R3)+,R1
	TST	(R1)+
	MOV	R2,-(SP)
	MOV	#16,R2
0E3:	CMPB	(R1)+,#'.
	BEQ	1E2
	SOB	R2,0E3
	MOV	(SP)+,R2
	BR	0E2
1E2:	MOV	(SP)+,R2
	MOVB	@R1,R0
	CALL	@120064
	CMPB	R0,@SP
	BNE	0E2
	MOV	-(R3),(R4)+
	INC	4(R5)
	TST	(R3)+
0E2:	SOB	R2,0E1
	INC	@SP
	CMP	@SP,#140
	BLOS	0E
	TST	(SP)+
1OK:	BR	2OK

0UNSRT:	MOV	(R3)+,(R4)+
	INC	4(R5)
	SOB	R2,0UNSRT
	BR	1OK

0NAME:	MOV	#41,-(SP)
0N:	MOV	#FILES,R3
	MOV	(R3)+,R2
0N1:	MOV	(R3)+,R1
	MOVB	2(R1),R0
	CALL	@120064
	CMPB	R0,@SP
	BNE	0N2
	MOV	R1,(R4)+
	INC	4(R5)
0N2:	SOB	R2,0N1
	INC	@SP
	CMP	@SP,#140
	BLOS	0N
	TST	(SP)+
	BR	1OK
TX:	MOV	@SP,R1
	ADD	(R1)+,@#160
	BR	1
OUTT:	MOV	@SP,R1
	BR	1
OUTT1:	MOV	@SP,R1
	MOV	(R1)+,@#160
1:	EMT	20
	INC	R1
	BIC	#1,R1
	MOV	R1,@SP
	RET

PUTC:	CALL	PUT
	MOV	@#160,-(SP)
	TRAP	TXT$
	.BYTE	11,11,11,40,0	.EVEN
	TRAP	SELP$
	CMPB	20(R5),#40
	BLOS	001
	COMB	@#44
001:	MOV	(SP)+,@#160
PUTI:	MOV	#34,R0
	SUB	FILES,R0
	ASR	R0
	ADD	R0,@#160
	BR	1P
PUT1:	CALL	PUT
1P:	MOV	#FILES,R1
	MOV	(R1)+,R2
	ADD	R1,R2
	CLRB	@R2
	EMT	20
	CLRB	@#44
	RET
PUT:	MOV	@#160,-(SP)
	TRAP	SELP$
	MOV	#FILES+2,R1
	MOV	#3,FILES
	MOVB	DEVTEK,R0
	ADD	#101,R0
	MOVB	R0,(R1)+
	MOVB	#':,(R1)+
	MOVB	#'\,(R1)+
	TST	LD0
	BEQ	01
	MOVB	#'.,(R1)+
	MOVB	#'.,(R1)+
	MOVB	#'.,(R1)+
	ADD	#3,FILES
01:	ADD	#20,R5
	CMPB	@R5,#40
	BLOS	03
	MOV	R5,R3
	SUB	#3,R3
	BR	05
03:	TRAP	FINS$
	BCS	0
05:	CMP	FILES,#3
	BLOS	02
	MOVB	#'\,(R1)+
	INC	FILES
02:	ADD	#3,R3
	MOV	#15,R2
	MOVB	(R3)+,(R1)+
	SOB	R2,.-2
	MOV	#15,R2
2:	CMPB	-(R1),#40
	BHI	1
	DEC	R2
	BR	2
1:	ADD	R2,FILES
0:	MOV	(SP)+,@#160
	RET

FINSUB:	TRAP	SELP$
	MOVB	SUBTEK,R0
	BEQ	1OK
FIN1:	MOV	30(R3),R2
	BEQ	2
	ADD	#450,R3
3:	ADD	#30,R3
	CMPB	#377,@R3
	BEQ	3
	CMPB	#177,2(R3)
	BNE	4
	CMPB	R0,@R3
	BEQ	0OK
4:	SOB	R2,3
2:	TST	(PC)+
0OK:	TST	(PC)+
1OK:	SEC
	RET

SWAPAN:	COM	TEKPAN
	TRAP	SELP$
	MOV	SUBTEK,R0
	SWAB	R0
	MOV	R0,SUBTEK
	MOVB	R0,SUBDIR
	MOV	SORTIR,R0
	SWAB	R0
	MOV	R0,SORTIR
	MOV	LD2,R0
	MOV	LD0,LD2
	MOV	R0,LD0
	MOV	R0,LD1
	RET

SELPAN:	MOV	#320,@#306
	TST	TEKPAN
	BNE	1
	MOVB	PANEL,DEVTEK
	MOV	#BLP1,R5
	MOV	#41200,@#160
	BR	2
1:	MOVB	PANEL+1,DEVTEK
	MOV	#BLP2,R5
	MOV	#41240,@#160
2:	MOV	BLK,R0
	MOVB	DEVTEK,34(R0)
	MOV	@R5,DIR
	MOV	2(R5),R4
	MOV	@R5,R3
	MOVB	SORTIR,R0
	BIC	#177774,R0
	BITB	#4,SORTIR
	BEQ	3
	BIS	#177400,R0
3:	RET

RAMKA:	TRAP	SELP$
	SWAB	R0
	MOV	R0,-(SP)
	MOV	@#160,R0
	MOV	#36,R1
	MOV	#210.,R2
	CALL	RAMK
	MOV	@#160,R0
	ADD	#2000,R0
	MOV	#36,R1
	CALL	S0
	MOV	#182.,R2
	MOV	#100,R4
	TSTB	@SP
	BEQ	11

12$:	MOVB	#1,20(R0)
	MOVB	#100,17(R0)
	ADD	R4,R0
	SOB	R2,12$
	BR	13$

11:	MOVB	#20,17(R0)
	MOVB	#20,26(R0)
	MOVB	#20,35(R0)
	ADD	R4,R0
	SOB	R2,11
13$:	TST	(SP)+
	BR	S0

LINII:	TRAP	SELP$
	SWAB	R0
	MOV	R0,-(SP)

1$:	MOV	@#160,R0
	ADD	#2000,R0
	MOV	#36,R1
	CALL	S0
	MOV	#182.,R2
	MOV	#100,R4
	TSTB	@SP
	BEQ	11

12$:	MOV	#17,R5
	TSTB	(R0)+
	CLRB	(R0)+
	CLRB	(R0)+
	SOB	R5,.-4
	SUB	#37,R0
	MOVB	#1,20(R0)
	MOVB	#100,17(R0)
	ADD	R4,R0
	SOB	R2,12$
	BR	13$

11:	MOV	#17,R5
	TSTB	(R0)+
	CLRB	(R0)+
	CLRB	(R0)+
	SOB	R5,.-4
	SUB	#37,R0
	MOVB	#20,17(R0)
	MOVB	#20,26(R0)
	MOVB	#20,35(R0)
	ADD	R4,R0
	SOB	R2,11
13$:	TST	(SP)+
	BR	S0

WIN:	MOV	@SP,R3
	ADD	#2,@SP
	MOVB	(R3)+,R1
	MOVB	(R3)+,R2
	MOV	R1,-(SP)
	MOV	R2,-(SP)
	CALL	RAMK
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	ADD	#1502,R0
	ADD	R1,R0
1:	CLRB	@R0
	ADD	#100,R0
	SOB	R2,1
	MOV	#10,R2
	INC	R0
	CMPB	(R1)+,(R1)+
2:	MOV	R1,R3
3:	CLRB	-(R0)
	SOB	R3,3
	SUB	#100,R0
	ADD	R1,R0
	SOB	R2,2
	RET

RAMK:;	MOV	R0,-(SP)
;	MOV	R2,R3
;	ASR	R3
;2:	ADD	#100,R0
;	SOB	R3,2
;	CLR	SDV1+2
;	CLR	SDV2+2
;	MOV	#2,-(SP)
;LL1:	MOV	R1,-(SP)
;	MOV	R2,-(SP)
;	MOV	4(SP),R2
;	ADD	#30,SDV1+2
;	ADD	#1400,SDV2+2
;	CALL	RMK
;	MOV	(SP)+,R2
;	MOV	(SP)+,R1
;SDV1:	ADD	#0,@SP
;SDV2:	SUB	#0,R0
;	CMP	@SP,R2
;	BLO	LL1
;	TST	(SP)+
;	MOV	(SP)+,R0

RMK:	MOV	R0,-(SP)
	CALL	S4
	CALL	S1
	CALL	S0
13:	MOV	#24,R5		;PAMKA
	MOV	R5,R4		;PAMKA
	CLR	R3
	CALL	S2
	SOB	R2,13
	CALL	S0
	CALL	S1
	CALL	S4
	MOV	(SP)+,R0
	RET
S4:	MOV	R1,R3
	MOVB	#5,-(SP)
	MOV	#120,R4
	MOV	#125,R5
	TSTB	@#45
	BEQ	1S1
	COMB	R4
	COMB	R5
	COMB	@SP
1S1:	MOVB	R4,(R0)+
	MOVB	R5,(R0)+
	SOB	R3,.-2
	MOVB	(SP)+,(R0)+
	BR	S3

S0:	MOV	#124,R4		;PAMKA
	MOV	#125,R3
	MOV	#25,R5		;PAMKA
	BR	S2
S1:	MOV	#4,R4		;PAMKA
	MOV	#20,R5		;PAMKA
	CLR	R3
S2:	TSTB	@#45
	BEQ	111
	COMB	R4
	COMB	R3
	COMB	R5
111:	MOVB	R4,(R0)+
	MOV	R1,R4
	ASR	R4
22:	MOVB	R3,(R0)+
	MOVB	R3,(R0)+
	SOB	R4,22
	BCC	23
	MOVB	R3,(R0)+
23:	MOVB	R5,(R0)+
S3:	ADD	#76,R0
	SUB	R1,R0
	RET

WEK274:	JSR	R4,@#110346
	MOV	R5,-(SP)
	CALL	INKEY
	CMPB	R0,#'0
	BLO	4
	CMPB	R0,#'9
	BHI	4
	BIC	#177400,R0
	SUB	#60,R0
5:	BISB	#200,R0
	BR	3
4:	CMPB	R0,#11
	BNE	2
3:	JMP	@#101622

2:	CMPB	R0,#12
	BEQ	5
	BISB	#200,R0
	CMPB	R0,#277
	BLOS	1
	JMP	@#101406
1:	JMP	@#101602

WEK60:	JSR	R4,@#110346
	MOV	R5,-(SP)
	CALL	INKEY1
	CMPB	R0,#'M-100
	BEQ	3
	CMPB	R0,#'P-100
	BEQ	3
	CMPB	R0,#16
	BNE	1
	MOVB	#200,@#43
	BR	3
1:	CMPB	R0,#17
	BNE	2
	CLRB	@#43
3:	CLR	@#222
	CLR	@#104
	CLR	R0
	JMP	@#101322
2:	JMP	@#101150
ZWUKI:	MOV	#400,R2
	BR	12
INKEY1:	TST	@#224
	BEQ	INKEY
	CALL	@#110536
INKEY:	MOV	#177777,@#222
	MOV	#10,R2
12:	MOV	#177716,R3
1:	MOV	@#102064,@R3
	MOV	#30,R0
	SOB	R0,.
	MOV	@#102076,@R3
	MOV	#30,R0
	SOB	R0,.
	SOB	R2,1
	MOV	@R3,R0
	MOVB	@#177662,R0
	RET

TRAP:	MOV	R5,-(SP)
	MOV	2(SP),R5
	MOV	-(R5),R5
	BIC	#177400,R5
	ADD	#TABTRP,R5
	MOV	@R5,R5
	MOV	2(SP),4(SP)
	MOV	R5,2(SP)
	MOV	(SP)+,R5
	JMP	@(SP)+

EMT:	MOV	R5,-(SP)
	MOV	2(SP),R5
	MOV	-(R5),R5
	CMP	R5,#104016
	BEQ	E16
	CMP	R5,#104020
	BEQ	E20
	BIC	#177400,R5
	CMP	R5,#200
	BLO	E4$
	BIC	#177600,R5
090:	ADD	#120000,R5
0100:	MOV	2(SP),4(SP)
	MOV	@R5,2(SP)
GO:	MOV	(SP)+,R5
	JMP	@(SP)+
E4$:	MOV	(SP)+,R5
	MOV	@#V30,-(SP)
	JMP	@(SP)+
E20:	MOVB	(R1)+,R0
	BEQ	EEX
	CMPB	R0,#11
	BHI	1
	MOVB	R0,R2
	MOV	#40,R0
2:	EMT	16
	SOB	R2,2
	BR	E20
1:	EMT	16
	BR	E20
E16:	CMPB	R0,#234
	BNE	001
	COMB	@#45
	BR	EEX
001:	CMPB	R0,#237
	BNE	002
	COMB	@#44
	BR	EEX
002:	CMPB	R0,#40
	BLO	E4$
	CMPB	R0,#200
	BLO	2
	CMPB	R0,#237
	BLOS	E4$
2:	CALL	EMT16
EEX:	MOV	(SP)+,R5
	RTI


EMT16:	MOV	R0,-(SP)
	MOV	R1,-(SP)
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R4,-(SP)
	MOV	R5,-(SP)
02:	BIC	#177400,R0
	TSTB	R0
	BPL	10
	SUB	#40,R0
10:	ASL 	R0
	MOV 	R0,R3
	ASL	R0
	ASL 	R0
	ADD 	R3,R0
	ADD	#112276-500,R0
03:	MOV	@#160,R3
	MOV	#100,R1
	CMP	R0,#112276
	BEQ	1
	MOVB	COLOR,R5
	BNE	0COL
	TSTB	@#44
	BEQ	1
0COL:	MOV	#5,R2
4I:	MOVB	(R0)+,R4
	MOVB	R4,@R3
	RORB	R4
	BISB	R4,(R3)
	BICB	R5,@R3
	ADD	R1,R3

	MOVB	(R0)+,R4
	MOVB	R4,@R3
	RORB	R4
	BISB	R4,(R3)
	BICB	R5,@R3
	ADD	R1,R3

	SOB	R2,4I
	BR	2
1:	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
	MOVB	(R0)+,@R3
	ADD	R1,R3
2:	TSTB	@#45
	BEQ	5
	MOV	#12,R2
	MOV	#100,R1
6:	SUB	R1,R3
	COMB	@R3
	SOB	R2,6
5:	INC	@#160
	INC	@#156
10K:	MOV	(SP)+,R5
	MOV	(SP)+,R4
	MOV	(SP)+,R3
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	RET

OCTOUT:	MOV	R5,-(SP)	;π/π πεώατι χοσψν.ώισμα
	MOV	R4,-(SP)
	CLR	R0
	CLR	R5
	ROL	R2
	ROL	R0
	TST	R0
	BEQ	1$
	ADD	#20,R0
	INC	R5
1$:	ADD	#40,R0
	CALL	EMT16
	MOV	#5,R4
5$:	CLR	R0
	ROL	R2
	ROL	R0
	ROL	R2
	ROL	R0
	ROL	R2
	ROL	R0
	TST	R0
	BNE	2$
	TST	R5
	BNE	3$
	ADD	#40,R0
	BR	4$
2$:	INC	R5
3$:	ADD	#60,R0
4$:	CALL	EMT16
	SOB	R4,5$
	TST	R5
	BNE	6$
	DEC	@#160
	MOV	#'0,R0
	CALL	EMT16
6$:	MOV	(SP)+,R4
	MOV	(SP)+,R5
	RET

STROKA:	MOV	@#160,PRENT
	MOV	R2,R5
	MOV	#FILES,R1
	MOV	R3,R0
	MOVB	(R0)+,(R1)+
	SOB	R5,.-2
	MOV	R2,R5
	ADD	R3,R2
	DEC	R2
	MOV	R3,R4
1:	MOV	@#160,-(SP)
	MOV	PRENT,@#160	;R2-END
	MOV	R3,R1		;R3-START
2:	CMP	R1,R2		;R4-TEK.
	BHI	3
	MOVB	(R1)+,R0
	EMT	TST.
	EMT	16
	BR	2
3:	MOV	(SP)+,@#160
0K:	CALL	KURSS
	EMT	EMT6.
	CALL	KURSS
	CMPB	R0,#30
	BNE	01
	CMP	R4,R3
	BEQ	1
	MOV	R4,R1
010:	MOVB	@R1,-1(R1)
	INC	R1
	CMP	R1,R2
	BLOS	010
	MOVB	#40,-(R1)
020:	DEC	R4
	DEC	@#160
	BR	1
01:	CMPB	R0,#10
	BNE	02
	CMP	R4,R3
	BHI	020
02:	CMPB	R0,#31
	BNE	03
030:	CMP	R4,R2
	BEQ	1
	INC	@#160
	INC	R4
	BR	1
03:	CMPB	R0,#12
	BNE	04
	CLC
	RET
04:	CMPB	R0,#3
	BNE	05
	MOV	#FILES,R1
	MOVB	(R1)+,(R3)+
	SOB	R5,.-2
	SEC
	RET
05:	CMPB	R0,#40
	BLO	0K
	CMPB	R0,#200
	BLO	06
	CMPB	R0,#240
	BLO	0K
06:	MOVB	R0,(R4)
	BR	030

KURSS:	MOV	R0,-(SP)
	MOV	@#160,R0
	COMB	1100(R0)
	COMB	1000(R0)
	MOV	(SP)+,R0
	RET

PLUS:	MOV	#52763,STO
	BR	1
MINUS:	MOV	#42763,STO
1:	TST	14(R5)
	BEQ	SUN
SU2:	TRAP	STA1$
SUN:	TRAP	TAKE$
	MOV	4(R5),R2
	BEQ	SU2
2:	CMP	(R4)+,R3
	BEQ	OOK1
	SOB	R2,2
	BR	SU2
OOK:	MOV	(R4)+,R3
OOK1:	CMPB	2(R3),#177
	BEQ	3
	CMP	R3,#LDISK
	BEQ	3
STO:	BIS	#100000,22(R3)
3:	SOB	R2,OOK
	TRAP	INVKR$
	TRAP	OUTP$
	TRAP	IS1$

PALIT:	CMPB	#300,@#177717
	BEQ	3
	TSTB	PALET
	BEQ	1C
	CLRB	PALET
	BR	2C
1C:	INCB	PALET
2C:	TRAP	NEBO$

3:	MOVB	PALET,R0
	CMPB	R0,#17
	BLO	1
	CLR	R0
	BR	2
1:	INCB	R0
2:	MOVB	R0,PALET
	CALL	MKEPL
	TRAP	STA1$

CLS:	MOV	#40000,R0
	MOV	#10000,R2
1:	CLR	(R0)+
	CLR	(R0)+
	SOB	R2,1
MKEPL:	CMP	#220,@#125302+2
	BEQ	2
	CLR	R0
	MOVB	PALET,R0
	SWAB	R0
	BIS	#40000,R0
	MOV	R0,@#177662
	RET
2:	CLRB	COLOR
	TSTB	PALET
	BEQ	3
	MOVB	#125,COLOR
3:	RET

NEBO:	TRAP	CLS$
	CLR	@#104
1:	MOV	#100140,R3
2:	CMP	R3,#112200
	BHIS	1
	MOV	40(R3),R1
	MOVB	#'.,R0
	TRAP	ZWE$
	MOV	(R3)+,R1
	MOV	#40,R0
	TRAP	ZWE$
	MOV	10(R3),R1
	MOVB	#'*,R0
	TRAP	ZWE$
	MOV	4(R3),R1
	MOVB	#'+,R0
	TRAP	ZWE$
	MTPS	#0
	TSTB	@#105
	BEQ	2
NEBO1:	TRAP	CLS$
	CALL	TEXTY
	TRAP	SWAP$
	TRAP	RAMA$
	TRAP	OUTP$
	TRAP	SWAP$
	TRAP	RAMA$
	TRAP	OUTP$
	CALL	PRIP
	TRAP	IS1$
IS:	TRAP	INVKR$
	TRAP	STA$

ZWEZ:	MOV	R1,R2
	SWAB	R2
	EMT	24
	CLR	@#156
	EMT	16
	MOV	PARP,R0
	BEQ	1
	CALL	@R0
	BR	2
1:	MOV	#25000,R0
	SOB	R0,.
2:	RET
.END
