10 OUT 145,6:OUT 147,15
20 DIM C(3,4),G(8),N$(8)
30 GOSUB 40:GOTO 90
40 CLS
50 LOCATE 16,2:PRINT"*******************************"
60 LOCATE 16,3:PRINT"*****   ЭЛЕКТРОН         ******"
70 LOCATE 16,4:PRINT"*******************************"
80 RETURN
90 LOCATE 10,6:PRINT"Электрон, вылетев из левого нижнего угла квадрата,"
100 LOCATE 10,7:PRINT"движется в электрическом поле, созданном случайно"
110 LOCATE 10,8:PRINT"расположенными положительными и отрицательными"
120 LOCATE 10,9:PRINT"ионами."
130 LOCATE 10,10:PRINT"Вы задаете вектор начальной скорости электрона,"
140 LOCATE 10,11:PRINT"стремясь к тому, чтобы точка выхода электрона из"
150 LOCATE 10,12:PRINT"поля оказалась возможно ближе к правому верхнему"
160 LOCATE 10,13:PRINT"углу квадрата."
170 LOCATE 10,14:PRINT"Количество очков в каждом раунде равно сумме коор-"
180 LOCATE 10,15:PRINT"динат Х и У точки выхода.Победитель определяется"
190 LOCATE 10,16:PRINT"по общему результату трех раундов."
200 LOCATE   10,17:PRINT"Вектор задается координатами вершины (Х и У) "
210 IF L=1 THEN GOTO 310
220 LOCATE 10,22:PRINT"сколько игроков (1-6)":INPUT K
230 IF K&lt;1 OR K>6 THEN GOTO 220
240 CLS:LOCATE 5,3:PRINT"введите имена игроков":LOCATE 5,4:PRINT"в каждом имени - не более 16 букв"
250 FOR I=1 TO  8:N$(I)=" ":NEXT I:FOR I=1 TO  K:LOCATE 5,5+I:INPUT N$(I):NEXT I:IF N$(1)=" " THEN GOTO 250
260 T=0:P0=8
270 FOR I=1 TO 8
280 IF MID$(N$(I),1,1)=" " THEN T=T+1
290 NEXT I
300 P0=P0-T
310 CLS
320 LOCATE 40,18:PRINT"игрок          очки":LOCATE 4,19:PRINT"раунд"
330 FOR I=1 TO P0:LOCATE 40,18+I:PRINT N$(I),"000":NEXT I   
340 FOR I=1 TO  8:G(I)=0:NEXT I  
350 N=17
360 FOR R=1 TO  3 
370 LOCATE 11,19:PRINT R
380 FOR P=1 TO  P0
390 GOSUB 530
400 GOSUB 600
410 LOCATE 4,20:PRINT"ходит",N$(P);"        "
420 LOCATE 4,21:PRINT"вектор скорости (х,у)":LOCATE 4,22:INPUT X:LOCATE 4,23 :INPUT Y
430 IF X&lt;0 OR X>20 THEN GOTO 420
440 IF Y&lt;0 OR Y>40 THEN GOTO 420
450 GOSUB 730
460 FOR J=1  TO  4:LOCATE C(1,J)+1,C(2,J)+1:PRINT" ":NEXT J
470 G(P)=G(P)+VYV:VYV=0
480 NEXT P
490 NEXT R
500 GOSUB 1150
510 CLS:LOCATE 10,7:PRINT"победил",P$
520 LOCATE 10,9:PRINT"игроки те же (1- да,0- нет)":INPUT L:GOTO 210
530 REM igrovoe pole
540 LINE (10,10)-(10,180),1
550 LINE (10,10)-(180,10),1
560 LINE (180,10)-(180,180),1
570 LINE (10,180)-(180,180),1:PAINT (11,11),2
580 LOCATE 26,18:PRINT"20 тыс.км":LOCATE 31,1:PRINT"40 тыс. км " 
590 RETURN
600 REM
610 FOR I=1 TO  2:FOR J=1 TO  4  
620 C(I,J)=INT((N-1)*RND(1)+1)
630 NEXT J,I
640 FOR J=1 TO  3 
650 FOR K=J+1 TO 4 
660 IF C(1,J)&lt;> C(1,K) THEN GOTO 690
670 IF C(2,J) &lt;> C(2,K) THEN GOTO 690
680 C(I,J)=INT ((N-1)*RND(1)) :GOTO 640
690 NEXT K,J
700 FOR J=1 TO 2:LOCATE C(1,J)+1,C(2,J)+1:PRINT"-":NEXT J 
710 FOR J= 3 TO  4:LOCATE C(1,J)+1,C(2,J)+1:PRINT"+":NEXT J
720 RETURN
730 REM
740 EX1=10:EY1=180:K=X/Y:X=2:Y=1/K
750 A=ATN(Y/X) 
760 C=SQR(Y^2+X^2)
770 IF EX1>180 OR EX1&lt;10 OR EY1>180 OR EY1&lt;10 THEN GOTO 880
780 PSET (EX1,EY1),3
790 IX=(C(1,3)+1)*6.25:IY=(C(2,3)+1)*10:Q=1
800 GOSUB 900
810 IX=(C(1,4)+1)*6.25:IY=(C(2,4)+1)*10:Q=1
820 GOSUB 900
830 IX=(C(1,1)+1)*6.25:IY=(C(2,1)+1)*10:Q=-1
840 GOSUB 900
850 IX=(C(1,2)+1)*6.25:IY=(C(2,2)+1)*10:Q=-1
860 GOSUB 900
870 GOTO 770
880 LOCATE 55,18+P:VYV=VYV+EX1+(180-EY1):VYV=INT(VYV):PRINT VYV:LOCATE 4,22:PRINT"    ":LOCATE 4,23:PRINT"    ":RETURN  
890 REM **shag elekt **
900 SX=IX-EX1:V=0
910 SY=IY-EY1
920 IF SY>0 AND SX>0 THEN PL=2
930 IF SY&lt;0 AND SX>0 THEN PL=1
940 IF SY>0 AND SX&lt;0 THEN PL=3
950 IF SY&lt;0 AND SX&lt;0 THEN PL=4
960 IF SX&lt;0 THEN SX=SX*(-1)
970 IF SY&lt;0 THEN SY=SY*(-1)
980 IF SX>20 OR SY >20 THEN GOTO 1120
990 IF SX>SY THEN V=1
1000 IF Q=1 AND PL=2 THEN A=A-1
1010  IF Q=-1 AND PL=1 AND V=1 THEN A=A+1
1020 IF Q=-1 AND PL =1 AND V=0 THEN A=A-1 
1030 IF Q=1 AND PL=1 AND V=1 THEN A=A-1
1040 IF Q=-1 AND PL=2 THEN A=A+1
1050 IF Q=1 AND PL=4 THEN A=A+1
1060 IF Q=-1 AND  PL=4 THEN A=A-1
1070 IF Q=1 AND PL=3 AND V=0 THEN A=A-1
1080 IF Q=-1 AND PL=3 AND V=0 THEN A=A+1
1090 IF Q=1 AND PL=3 AND V=1 THEN A=A+1
1100 IF Q=-1 AND PL=3 AND V=1 THEN A=A-1
1110 IF Q=1 AND PL=1 AND V=0 THEN A=A+1
1120 EX1=C*COS(A)+EX1
1130 EY1=EY1-C*SIN(A)
1140 RETURN
1150 P$=N$(1)
1160 FOR I=1 TO P0-1
1170 J=I+1
1180 IF G(I)&lt;G(J) THEN P$=N$(J)
1190 NEXT I
1200 RETURN
1210 END