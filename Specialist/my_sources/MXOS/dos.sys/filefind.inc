;+---------------------------------------------------------------------------
; MXOS
; Найти файл
;
; На входе
;  hl - имя
;
; На выходе
;  bc,de,hl - сохраняются
;  cf - ошибка
;  v_foundedFile - найденный файл
;
; 2013-12-12 Дизассемблировано vinxru
;----------------------------------------------------------------------------

fileFind:	; Загрузка FAT и корневого каталога в память
		call	loadFatDir

fileFind2:	; Сохраняем регистры
		push	b
		push	d

		; Начало каталога
		lxi	d, diskDirectory

		; Кол-во файлов в каталоге
		mvi	b, 48

fileFind_loop:	; Сохраняем адрес начала имени в каталоге и образец
		push	h
		push	d

		; Если первый символ имени 0FFh, переходим к следующем файлу
		ldax	d
		inr	a
		jz	fileFind_next

		; Длина имени 9 символов
		mvi	c, 9

fileFind_name:	; Если символы не равны, переходим к следующему файлу
		ldax	d
		cmp	m
		jnz	fileFind_next
		inx	h
		inx	d

		; Цикл
		dcr	c
		jnz	fileFind_name

		; Сохраняем адрес найденного файла
		pop	h
		shld	v_foundedFile

		; Восстаналиваем регистры и выходим с CF=0
		pop	h
		ora	a	; х.з.
		pop	d
		pop	b
		ret

; ---------------------------------------------------------------------------

fileFind_next:	; Вычисляем адрес следующего файла
		pop	d
		lxi	h, 10h
		dad	d
		xchg

		; Восстанавливаем образец
		pop	h

		; Еще остались файлы?
		dcr	b
		jnz	fileFind_loop

		; Восстаналиваем регистры и выходим с CF=1 и A=3
		pop	d
		pop	b
		mvi	a, 3
		stc
		ret