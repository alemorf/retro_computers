;+---------------------------------------------------------------------------
; MXOS
; Вывод 16-ричного числа на экран (слово)
;
; На входе
;  hl - число
;
; На выходе
;  все регистры сохраняются
;
; 2013-12-12 Дизассемблировано vinxru
;----------------------------------------------------------------------------

printHexWord:	; Сохраняем регистры
		push	psw		

		; Старший байт
		mov	a, h
		call	printHexByte

		; Младшйи байт
		mov	a, l
		call	printHexByte

		; Восстаналиваем регистры
		pop	psw
		ret

;----------------------------------------------------------------------------
; Вывод 16-ричного числа на экран (байт)
;
; На входе
;  a - число
;
; На выходе
;  все регистры сохраняются
;----------------------------------------------------------------------------

printHexByte:	; Сохраняем регистры
		push	b
		push	psw

		; Сохраняем A для вывода второй цифры
		mov	b, a

		; Первая цифра - старшие 4 бита
		rlc
		rlc
		rlc
		rlc
		call	printHexNibble

		; Вторая цифра - младщие 4 бита
		mov	a, b
		call	printHexNibble

		; Восстаналиваем регистры
		pop	psw
		pop	b
		ret

;----------------------------------------------------------------------------
; Вывод 16-ричного числа на экран (цифра)
;
; На входе
;  a - число, используются младшие 4 бита
;
; На выходе
;  c, a - изменяются, остальные регистры сохраняются
;----------------------------------------------------------------------------

printHexNibble:	ani	0Fh
		adi	'0'
		cpi	'9'+1
		jc	printHexNib_0
		  adi	'A'-'0'-10
printHexNib_0:	mov	c, a
		jmp	j_printChar